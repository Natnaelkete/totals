buildscript {
    ext.kotlin_version = '2.1.0'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
    configurations.all {
        resolutionStrategy {
            force 'org.jetbrains.kotlin:kotlin-stdlib:1.8.10'
            force 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.10'
            force 'androidx.window:window:1.0.0'
            force 'androidx.window:window-java:1.0.0'
            force 'androidx.activity:activity:1.9.3'
            force 'androidx.activity:activity-ktx:1.9.3'
        }
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')

    def applyNamespaceWorkaround = { proj ->
        if (proj.hasProperty('android')) {
            if (proj.android.getNamespace() == null) {
                def manifestFile = proj.file('src/main/AndroidManifest.xml')
                if (manifestFile.exists()) {
                    def manifest = new XmlParser().parse(manifestFile)
                    def packageName = manifest.attribute('package')
                    if (packageName) {
                        proj.android.namespace = packageName
                    }
                }
            }
        }
    }

    if (project.state.executed) {
        applyNamespaceWorkaround(project)
    } else {
        project.afterEvaluate { applyNamespaceWorkaround(it) }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
